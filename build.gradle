/*
 * Adapted from the Wizardry License
 *
 * Copyright (c) 2018-2019 DaPorkchop_ and contributors
 *
 * Permission is hereby granted to any persons and/or organizations using this software to copy, modify, merge, publish, and distribute it. Said persons and/or organizations are not allowed to use the software or any derivatives of the work for commercial use or any other means to generate income, nor are they allowed to claim this software as their own.
 *
 * The persons and/or organizations are also disallowed from sub-licensing and/or trademarking this software without explicit permission from DaPorkchop_.
 *
 * Any persons and/or organizations using this software must disclose their source code and have it publicly available, include this license, provide sufficient credit to the original authors of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NON INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

plugins {
    id 'java'
    id 'io.franzbecker.gradle-lombok' version '1.14' apply false
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '2.0.4' apply false
}

dependencies {
    for (Project prj : project.allprojects) {
        if (!"PorkLib".equalsIgnoreCase(prj.name) && prj.parent.name.equalsIgnoreCase("PorkLib")) {
            compile project(":" + prj.name)
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    group 'net.daporkchop.lib'
    version '0.3.0-SNAPSHOT'

    sourceCompatibility = 1.8

    ext {
        apacheCommonsCompressVersion = '1.16.1'
        bouncycastleVersion = '1.59'
        googleHttpClientVersion = '1.23.0'
        gsonVersion = '2.8.5'
        guavaVersion = '27.0-jre'
        jnaVersion = '5.3.0'
        junitVersion = '4.12'
        nettyVersion = '4.1.36.Final'
        raknetVersion = '1.3.4'
        sparseBitSetVersion = '1.1'
        xzVersion = '1.8'
    }

    repositories {
        mavenLocal()
        maven {
            name = "DaPorkchop_"
            url = "https://maven.daporkchop.net/"
        }
        maven {
            name = "Sonatype Snapshots"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "NukkitX Snapshots"
            url = "https://repo.nukkitx.com/maven-releases/"
        }
        mavenCentral()
    }

    jar {
        from project.sourceSets.main.output
        from(project.sourceSets.main.allSource) {
            include '**/*.xml'
        }
        baseName = archivesBaseName
    }

    configurations {
        provided
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
        example {
            java {
                compileClasspath += main.output
                compileClasspath += main.compileClasspath
                runtimeClasspath += main.output
                runtimeClasspath += main.runtimeClasspath
            }
        }
    }

    dependencies {
        if (!("root".equalsIgnoreCase(project.name) || "common".equalsIgnoreCase(project.name) || "unsafe".equalsIgnoreCase(project.name))) {
            compile project(":common")
        }
        if (!"logging".equalsIgnoreCase(project.name)) {
            testCompile project(":logging")
        }

        testCompile "junit:junit:$junitVersion"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'net.daporkchop.lib'
                artifactId = "PorkLib".equalsIgnoreCase(project.name) ? "full" : project.name
                version = project.version

                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
                artifact shadowJar {
                    classifier "full"
                }
            }
        }
    }

    build.dependsOn(publishToMavenLocal)

    test {
        testLogging {
            //show System.out calls in tests in gradle output
            showStandardStreams = true

            showExceptions = true
            showStackTraces = true
            showCauses = true
        }
    }
}
